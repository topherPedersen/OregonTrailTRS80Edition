1 PE$="PRESS <ENTER> TO CONTINUE"
2 PH=INT((RND(0)*100)+1)
3 IF PH<51 THEN DH=51
4 IF PH>50 THEN DH=34
5 DIM S$(4)
6 S$(1)="BANG"
7 S$(2)="BLAM"
8 S$(3)="POW"
9 S$(4)="WHAM"
10 PRINT "DO YOU NEED INSTRUCTIONS"
11 PRINT "(YES/NO)";
12 C$="YES/NO"
13 INPUT C$
14 IF C$="NO" THEN 43
15 PRINT
16 PRINT "THIS PROGRAM SIMULATES A 2,040"
17 PRINT "MILE TRIP OVER THE OREGON TRAIL"
18 PRINT "FROM INDEPENDENCE, MO TO"
19 PRINT "OREGON CITY, OR IN 1847."
20 PRINT: PRINT PE$: INPUT CT$: PRINT
21 PRINT "YOU'VE SAVED $700 TO SPEND FOR"
22 PRINT "THE TRIP. YOU WILL NEED TO"
23 PRINT "PURCHASE THE FOLLOWING"
24 PRINT "SUPPLIES:"
25 PRINT
26 PRINT "  * OXEN (SPEND $200-$300)"
27 PRINT "  * FOOD"
28 PRINT "  * AMMUNITION"
29 PRINT "  * CLOTHING" 
30 PRINT "  * MISC. SUPPLIES"
31 PRINT: PRINT PE$: INPUT CT$: PRINT
32 PRINT "YOU CAN SPEND ALL YOUR MONEY"
33 PRINT "NOW, OR SAVE SOME FOR ALONG"
34 PRINT "THE WAY"
35 PRINT: PRINT PE$: INPUT CT$: PRINT
36 REM X
37 PRINT "WHENEVER YOU HAVE TO USE YOUR"
38 PRINT "RIFLE, YOU WILL BE ASKED TO"
39 PRINT "TYPE IN A WORD (ONE THAT SOUNDS"
40 PRINT "LIKE A GUNSHOT). THEN PRESS"
41 PRINT "<ENTER> TO FIRE."
42 PRINT: PRINT PE$: INPUT CT$: PRINT
43 D9=1
44 IF D9>5 THEN 46
45 GOTO 47
46 D9=D3=M9=M=F2=F1=S4=K8=0
47 X1=-1
48 PRINT
49 Z9$="HOW MUCH DO YOU WANT TO SPEND"
50 PRINT Z9$: PRINT "ON YOUR OXEN TEAM"
51 INPUT A
52 IF A >= 200 THEN 55
53 PRINT "NOT ENOUGH"
54 GOTO 50
55 IF A <= 300 THEN 58
56 PRINT "TOO MUCH"
57 GOTO 50
58 PRINT Z9$: PRINT "ON FOOD"
59 INPUT F
60 IF F >= 0 THEN 63
61 PRINT "IMPOSSIBLE"
62 GOTO 58
63 PRINT Z9$: PRINT "ON AMMUNITION"
64 INPUT B
65 IF B >= 0 THEN 68
66 PRINT "IMPOSSIBLE"
67 GOTO 63
68 PRINT Z9$: PRINT "ON CLOTHING"
69 INPUT C
70 IF C >= 0 THEN 73
71 PRINT "IMPOSSIBLE"
72 GOTO 68
73 PRINT Z9$: PRINT "ON MISCELLANEOUS SUPPLIES"
74 INPUT M1
75 IF M1 >= 0 THEN 78
76 PRINT "IMPOSSIBLE"
77 GOTO 73
78 T=700-A-F-B-C-M1
79 IF T >= 0 THEN 82
80 PRINT "YOU OVERSPENT--YOU ONLY HAD": PRINT "$700 TO SPEND.  BUY AGAIN"
81 GOTO 48
82 B=50*B
83 PRINT "AFTER ALL YOUR PURCHASES. YOU": PRINT "NOW HAVE ";T;" DOLLARS LEFT"
84 PRINT
85 PRINT "MON MAR 29 1847"
86 PRINT
87 GOTO 121
88 IF M >= 2040 THEN 481
89 PRINT: PRINT PE$: INPUT CT$: PRINT
90 D3=D3+1
91 PRINT
92 PRINT "MON ";
93 IF D3>10 THEN 95
94 ON D3 GOTO 97, 98, 99, 100, 101, 102, 103, 104, 105, 106
95 ON D3-10 GOTO 107, 108, 109, 110, 111, 112, 113, 114, 115, 116
96 YR$="1847"
97 PRINT "APR 12 "YR$: GOTO 120
98 PRINT "APR 26 "YR$: GOTO 120
99 PRINT "MAY 10 "YR$: GOTO 120
100 PRINT "MAY 24 "YR$: GOTO 120
101 PRINT "JUN 7 "YR$: GOTO 120
102 PRINT "JUN 21 "YR$: GOTO 120
103 PRINT "JUL 5 "YR$: GOTO 120
104 PRINT "JUL 19 "YR$: GOTO 120
105 PRINT "AUG 2 "YR$: GOTO 120
106 PRINT "AUG 16 "YR$: GOTO 120
107 PRINT "AUG 31 "YR$: GOTO 120
108 PRINT "SEP 13 "YR$: GOTO 120
109 PRINT "SEP 27 "YR$: GOTO 120
110 PRINT "OCT 11 "YR$: GOTO 120
111 PRINT "OCT 25 "YR$: GOTO 120
112 PRINT "NOV 8 "YR$: GOTO 120
113 PRINT "NOV 22 "YR$: GOTO 120
114 PRINT "DEC 6 "YR$: GOTO 120
115 PRINT "DEC 20 "YR$: GOTO 120
116 PRINT "YOU HAVE BEEN ON THE TRAIL TOO"
117 PRINT "LONG--- YOUR FAMILY DIES IN THE"
118 PRINT "FIRST BLIZZARD OF WINTER"
119 GOTO 468
120 PRINT
121 IF F >= 0 THEN 123
122 F=0
123 IF B >= 0 THEN 125
124 B=0
125 IF C >= 0 THEN 127
126 C=0
127 IF M1 >= 0 THEN 129
128 M1=0
129 IF F >= 13 THEN 131
130 PRINT "YOU'D BETTER DO SOME HUNTING OR": PRINT "BUY FOOD AND SOON!!!!"
131 F=INT(F)
132 B=INT(B)
133 C=INT(C)
134 M1=INT(M1)
135 T=INT(T)
136 M=INT(M)
137 M2=M
138 IF S4=1 THEN 141
139 IF K8=1 THEN 141
140 GOTO 146
141 T=T-20
142 IF T<0 THEN 459
143 PRINT "DOCTOR'S BILL IS $20"
144 S4=0
145 K8=S4
146 IF M9=1 THEN 149
147 PRINT "TOTAL MILEAGE: ";M
148 GOTO 151
149 PRINT "TOTAL MILEAGE: 950"
150 M9=0
151 PRINT "FOOD: ";F
152 PRINT "BULLETS: ";B
153 PRINT "CLOTHING: ";C
154 PRINT "MISC. SUPPLIES: ";M1
155 PRINT "CASH: $";T
156 PRINT
157 IF X1=-1 THEN 167
158 X1=X1*(-1)
159 PRINT "DO YOU WANT TO:"
160 PRINT "  (1) STOP AT THE NEXT FORT"
161 PRINT "  (2) HUNT"
162 PRINT "  (3) CONTINUE"
163 INPUT X
164 GOTO 179
165 X=3
166 GOTO 179
167 PRINT "DO YOU WANT TO:"
168 PRINT "  (1) HUNT"
169 PRINT "  (2) CONTINUE"
170 INPUT X
171 IF X=1 THEN 173
172 X=2
173 X=X+1
174 IF X=3 THEN 178
175 IF B>39 THEN 178
176 PRINT "TOUGH---YOU NEED MORE BULLETS": PRINT "TO GO HUNTING"
177 GOTO 167
178 X1=X1*(-1)
179 ON X GOTO 180,204,221
180 PRINT "ENTER WHAT YOU WISH TO SPEND ON": PRINT "THE FOLLOWING..."
181 PRINT "FOOD";
182 GOSUB 184
183 GOTO 192
184 INPUT P
185 IF P<0 THEN 191
186 T=T-P
187 IF T >= 0 THEN 191
188 PRINT "YOU DON'T HAVE THAT MUCH--KEEP": PRINT "YOUR SPENDING DOWN"
189 T=T+P
190 P=0
191 RETURN
192 F=F+2/3*P
193 PRINT "AMMUNITION";
194 GOSUB 184
195 B=INT(B+2/3*P*50)
196 PRINT "CLOTHING";
197 GOSUB 184
198 C=C+2/3*P
199 PRINT "MISCELLANEOUS SUPPLIES";
200 GOSUB 184
201 M1=M1+2/3*P
202 M=M-45
203 GOTO 221
204 IF B>39 THEN 207
205 PRINT "TOUGH---YOU NEED MORE BULLETS": PRINT "TO GO HUNTING"
206 GOTO 159
207 M=M-45
208 GOSUB 537
209 IF B1 <= 3 THEN 215
210 IF 100+RND(0)<13*B1 THEN 220
211 F=F+48-2*B1
212 PRINT "NICE SHOT--RIGHT ON TARGET--GOOD": PRINT "EATIN' TONIGHT!!"
213 B=B-10-3*B1
214 GOTO 221
215 PRINT "RIGHT BETWEEN THE EYES---YOU"
216 PRINT "GOT A BIG ONE! FULL BELLIES": PRINT "TONIGHT!": PRINT
217 F=F+52+RND(0)*6
218 B=B-10-RND(0)*4
219 GOTO 221
220 PRINT "YOU MISSED---AND YOUR DINNER": PRINT "GOT AWAY.....": PRINT
221 IF F >= 13 THEN 223
222 GOTO 457
223 PRINT "DO YOU WANT TO EAT:"
224 PRINT "  (1) POORLY"
225 PRINT "  (2) MODERATELY"
226 PRINT "  (3) WELL"
227 INPUT E
228 IF E>3 THEN 223
229 IF E<1 THEN 223
230 E=INT(E)
231 F=F-8-5*E
232 IF F >= 0 THEN 236
233 F=F+8+5*E
234 PRINT "YOU CAN'T EAT THAT WELL"
235 GOTO 223
236 M=M+200+(A-220)/5+10*RND(0)
237 C1=0
238 L1=C1
239 IF RND(0)*10>((M/100-4)*(M/100-4)+72)/((M/100-4)*(M/100-4)+12)-1 THEN 308
240 PRINT
241 PRINT "RIDERS AHEAD. THEY ";
242 S5=0
243 IF RND(0)<.8 THEN 246
244 PRINT "DON'T ";
245 S5=1
246 PRINT "LOOK": PRINT "HOSTILE": PRINT
247 PRINT "TACTICS:"
248 PRINT "  (1) RUN"
249 PRINT "  (2) ATTACK"
250 PRINT "  (3) CONTINUE"
251 PRINT "  (4) CIRCLE WAGONS"
252 IF RND(0)>.2 THEN 254
253 S5=1-S5
254 INPUT T1
255 IF T1<1 THEN 248
256 IF T1>4 THEN 248
257 T1=INT(T1)
258 IF S5=1 THEN 287
259 IF T1>1 THEN 265
260 M=M+20
261 M1=M1-15
262 B=B-150
263 A=A-40
264 GOTO 301
265 IF T1>2 THEN 278
266 GOSUB 537
267 B=B-B1*40-80
268 IF B1>1 THEN 271
269 PRINT "NICE SHOOTING--- YOU DROVE THEM": PRINT "OFF": PRINT
270 GOTO 301
271 IF B1 <= 4 THEN 276
272 PRINT "LOUSY SHOT---YOU GOT KNIFED"
273 K8=1
274 PRINT "YOU HAVE TO SEE OL' DOC": PRINT "BLANCHARD": PRINT
275 GOTO 301
276 PRINT "KINDA SLOW WITH YOUR COLT .45": PRINT
277 GOTO 301
278 IF T1>3 THEN 283
279 IF RND(0)>.8 THEN 299
280 B=B-150
281 M1=M1-15
282 GOTO 301
283 GOSUB 537
284 B=B-B1*30-80
285 M=M-25
286 GOTO 268
287 IF T1>1 THEN 291
288 M=M+15
289 A=A-10
290 GOTO 301
291 IF T1>2 THEN 295
292 M=M-5
293 B=B-100
294 GOTO 301
295 IF T1>3 THEN 297
296 GOTO 301
297 M=M-20
298 GOTO 301
299 PRINT "THEY DID NOT ATTACK"
300 GOTO 308
301 IF S5=0 THEN 304
302 PRINT: PRINT "RIDERS WERE FRIENDLY, BUT CHECK": PRINT "FOR POSSIBLE LOSSES": PRINT
303 GOTO 308
304 PRINT: PRINT "RIDERS WERE HOSTILE--CHECK FOR": PRINT "LOSES": PRINT
305 IF B >= 0 THEN 308
306 PRINT: PRINT "YOU RAN OUT OF BULLETS AND GOT": PRINT "MASSACRED BY THE RIDERS": PRINT
307 GOTO 468
308 D1=0
309 RESTORE
310 R1=100*RND(0)
311 D1=D1+1
312 IF D1=16 THEN 420
313 READ D
314 IF R1>D THEN 311
315 DATA 6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
316 IF D1>10 THEN 318
317 ON D1 GOTO 319,323,327,333,336,339,342,349,356,372
318 ON D1-10 GOTO 375,382,387,409,414,420
319 PRINT: PRINT "WAGON BREAKS DOWN--LOSE TIME": PRINT "AND SUPPLIES FIXING IT": PRINT
320 M=M-15-5*RND(0)
321 M1=M1-8
322 GOTO 423
323 PRINT: PRINT "0X INJURES LEG---SLOWS YOU DOWN": PRINT "REST OF TRIP": PRINT
324 M=M-25
325 A=A-20
326 GOTO 423
327 PRINT: PRINT "BAD LUCK---YOUR DAUGHTER BROKE"
328 PRINT "HER ARM. YOU HAD TO STOP AND"
329 PRINT "USE SUPPLIES TO MAKE A SLING.": PRINT
330 M=M-5-4*RND(0)
331 M1=M1-2-3*RND(0)
332 GOTO 423
333 PRINT: PRINT "OX WANDERS OFF---SPEND TIME": PRINT "LOOKING FOR IT": PRINT
334 M=M-17
335 GOTO 423
336 PRINT: PRINT "YOUR SON GETS LOST---SPEND": PRINT "HALF THE DAY LOOKING FOR HIM": PRINT
337 M=M-10
338 GOTO 423
339 PRINT: PRINT "UNSAFE WATER--LOSE TIME LOOKING": PRINT "FOR CLEAN SPRING": PRINT
340 M=M-10*RND(0)-2
341 GOTO 423
342 IF M>950 THEN 402
343 PRINT: PRINT "HEAVY RAINS---TIME AND SUPPLIES": PRINT "LOST": PRINT
344 F=F-10
345 B=B-500
346 M1=M1-15
347 M=M-10*RND(0)-5
348 GOTO 423
349 PRINT: PRINT "BANDITS ATTACK": PRINT
350 GOSUB 552
351 B=B-20*B1
352 IF B >= 0 THEN 356
353 PRINT: PRINT "YOU RAN OUT OF BULLETS---THEY": PRINT "GET LOTS OF CASH": PRINT
354 T=T/3
355 GOTO 357
356 IF B1 <= 1 THEN 363
357 PRINT: PRINT "YOU GOT SHOT IN THE LEG AND": PRINT "THEY TOOK ONE OF YOUR OXEN": PRINT
358 K8=1
359 PRINT: PRINT "BETTER HAVE A DOC LOOK AT YOUR": PRINT "WOUND": PRINT
360 M1=M1-5
361 A=A-20
362 GOTO 423
363 PRINT: PRINT "QUICKEST DRAW OUTSIDE OF DODGE"
364 PRINT "CITY!!! YOU GOT 'EM!": PRINT
365 GOTO 423
366 PRINT: PRINT "THERE WAS A FIRE IN YOUR WAGON,": PRINT "FOOD AND SUPPLIES DAMAGE!": PRINT
367 F=F-40
368 B=B-400
369 M1=M1-RND(0)*8-3
370 M=M-15
371 GOTO 423
372 PRINT: PRINT "LOSE YOUR WAY IN HEAVY FOG,": PRINT "TIME IS LOST": PRINT
373 M=M-10-5*RND(0)
374 GOTO 423
375 PRINT: PRINT "YOU KILLED A POISONOUS SNAKE": PRINT "AFTER IT BIT YOU": PRINT
376 B=B-10
377 M1=M1-5
378 IF M1 >= 0 THEN 381
379 PRINT: PRINT "YOU DIE OF SNAKEBITE SINCE YOU": PRINT "HAVE NO MEDICINE": PRINT
380 GOTO 468
381 GOTO 423
382 PRINT: PRINT "WAGON GETS SWAMPED FORDING": PRINT "RIVER--LOSE FOOD AND CLOTHES": PRINT
383 F=F-30
384 C=C-20
385 M=M-20-20*RND(0)
386 GOTO 423
387 PRINT: PRINT "WILD ANIMALS ATTACK!": PRINT
388 GOSUB 552
389 IF B>39 THEN 394
390 PRINT: PRINT "YOU WERE TOO LOW ON BULLETS--"
391 PRINT "THE WOLVES OVERPOWERED YOU": PRINT
392 K8=1
393 GOTO 463
394 IF B1>2 THEN 397
395 PRINT: PRINT "NICE SHOOTIN' PARDNER---THEY": PRINT "DIDN'T GET MUCH": PRINT
396 GOTO 398
397 PRINT: PRINT "SLOW ON THE DRAW---THEY GOT AT": PRINT "YOUR FOOD AND CLOTHES": PRINT
398 B=B-20*B1
399 C=C-B1*4
400 F=F-B1*8
401 GOTO 423
402 PRINT: PRINT "COLD WEATHER---BRRRRRRR!---YOU"
403 IF C>22+4*RND(0) THEN 406
404 PRINT "DONT'T ";
405 C1=1
406 PRINT "HAVE ENOUGH CLOTHING TO KEEP": PRINT "YOU WARM": PRINT
407 IF C1=0 THEN 423
408 GOTO 568
409 PRINT: PRINT "HAIL STORM---SUPPLIES DAMAGED": PRINT
410 M=M-5-RND(0)*10
411 B=B-200
412 M1=M1-4-RND(0)*3
413 GOTO 423
414 IF E=1 THEN 568
415 IF E=3 THEN 418
416 IF RND(0)>.25 THEN 568
417 GOTO 423
418 IF RND(0)<.5 THEN 568
419 GOTO 423
420 PRINT: PRINT "HELPFUL INDIANS SHOW YOU WHERE": PRINT "TO FIND MORE FOOD": PRINT
421 F=F+14
422 GOTO 423
423 IF M <= 950 THEN 88
424 IF RND(0)*10>9-((M/100-15)*(M/100-15)+72)/((M/100-15)*(M/100-15)+12) THEN 438
425 PRINT: PRINT "RUGGED MOUNTAINS": PRINT
426 IF RND(0)>.1 THEN 430
427 PRINT: PRINT "YOU GOT LOST---LOSE VALUABLE": PRINT "TIME TRYING TO FIND TRAIL!": PRINT
428 M=M-60
429 GOTO 438
430 IF RND(0)>.11 THEN 436
431 PRINT: PRINT "WAGON DAMAGED!---LOSE TIME AND": PRINT "SUPPLIES": PRINT
432 M1=M1-5
433 B=B-200
434 M=M-20-30*RND(0)
435 GOTO 438
436 PRINT: PRINT "THE GOING GETS SLOW": PRINT
437 M=M-45-RND(0)/.02
438 IF F1=1 THEN 442
439 F1=1
440 IF RND(0)<.8 THEN 449
441 PRINT: PRINT "YOU MADE IT SAFELY THROUGH": PRINT "SOUTH PASS--NO SNOW": PRINT
442 IF M<1700 THEN 446
443 IF F2=1 THEN 446
444 F2=1
445 IF RND(0)<.7 THEN 449
446 IF M>950 THEN 88
447 M9=1
448 GOTO 88
449 PRINT: PRINT "BLIZZARD IN MOUNTAIN PASS--TIME": PRINT "AND SUPPLIES LOST": PRINT
450 L1=1
451 F=F-25
452 M1=M1-10
453 B=B-300
454 M=M-30-40*RND(0)
455 IF C<18+2*RND(0) THEN 568
456 GOTO 446
457 PRINT: PRINT "YOU RAN OUT OF FOOD AND STARVED": PRINT "TO DEATH": PRINT
458 GOTO 468
459 T=0
460 PRINT: PRINT "YOU CAN'T AFFORD A DOCTOR"
461 GOTO 463
462 PRINT: PRINT "YOU RAN OUT OF MEDICAL SUPPLIES"
463 PRINT "YOU DIED OF ";
464 IF K8=1 THEN 467
465 PRINT "PNEUMONIA..."
466 GOTO 468
467 PRINT "INJURIES..."
468 PRINT: PRINT PE$: INPUT CT$: PRINT
469 PRINT "DEAR INTREPID TRAVELER,"
470 PRINT
471 PRINT "WE ARE DEEPLY SADDENED THAT"
472 PRINT "YOU DIDN'T MAKE IT TO THE GREAT"
473 PRINT "TERRITORY OF OREGON. BETTER"
474 PRINT "LUCK NEXT TIME."
475 PRINT
476 PRINT "SINCERELY,"
477 PRINT
478 PRINT "THE OREGON CITY"
479 PRINT "CHAMBER OF COMMERCE"
480 END
481 F9=(2040-M2)/(M-M2)
482 F=F+(1-F9)*(8+5*E)
483 PRINT: PRINT "YOU FINALLY ARRIVED IN OREGON!"
484 PRINT: PRINT PE$: INPUT CT$: PRINT
485 GOTO 529
486 F9=INT(F9*14)
487 D3=D3*14+F9
488 F9=F9+1
489 IF F9<8 THEN 573
490 F9=F9-7
491 IF D3>124 THEN 495
492 D3=D3-93
493 PRINT "JUL ";D3;" 1847"
494 GOTO 513
495 IF D3>155 THEN 499
496 D3=D3-124
497 PRINT "AUG ";D3;" 1847"
498 GOTO 513
499 IF D3>185 THEN 503
500 D3=D3-155
501 PRINT "SEP ";D3;" 1847"
502 GOTO 513
503 IF D3>216 THEN 507
504 D3=D3-185
505 PRINT "OCT ";D3;" 1847"
506 GOTO 513
507 IF D3>246 THEN 511
508 D3=D3-216
509 PRINT "NOV ";D3;" 1847"
510 GOTO 513
511 D3=D3-246
512 PRINT "DEC ";D3;" 1847"
513 PRINT
514Â IF B>0 THEN 516
515 B=0
516 IF C>0 THEN 518
517 C=0
518 IF M1>0 THEN 520
519 M1=0
520 IF T>0 THEN 522
521 T=0
522 IF F>0 THEN 524
523 F=0
524 PRINT "FOOD: ";INT(F)
525 PRINT "BULLETS: ";INT(B)
526 PRINT "CLOTHING: ";INT(C)
527 PRINT "MISC. SUPP.: ";INT(M1)
528 PRINT "CASH: ";INT(T)
529 PRINT
530 PRINT "PRESIDENT JAMES K. POLK SENDS"
531 PRINT "YOU HIS HEARTIEST"
532 PRINT "CONGRATULATIONS AND WISHES YOU"
533 PRINT "A PROSPEROUS LIFE AHEAD AT YOUR"
534 PRINT "NEW HOME"
535 END
536 PRINT
537 S6=INT(RND(0)*4+1)
538 PRINT "TYPE "; S$(S6)
539 B3=30000
540 INPUT C$
541 B4=(RND(0)*100)+1
542 IF B4<DH THEN B1=B3+0
543 IF B4<DH THEN 545
544 B1=B3+15
545 B1=((B1-B3)*3600)
546 PRINT
547 IF B1>0 THEN 549
548 B1=0
549 IF C$=S$(S6) THEN 551
550 B1=9
551 RETURN
552 REM X
553 S6=INT(RND(0)*4+1)
554 PRINT "TYPE "; S$(S6)
555 B3=30000
556 INPUT C$
557 B4=RND(0)*100+1
558 IF B4<86 THEN B1=B3+0
559 IF B4<86 THEN 561
560 B1=B3+15
561 B1=((B1-B3)*3600)
562 PRINT
563 IF B1>0 THEN 565
564 B1=0
565 IF C$=S$(S6) THEN 567
566 B1=9
567 RETURN
568 IF 100*RND(0)<10+35*(E-1) THEN 578
569 IF E=1 THEN E2=40/1
570 IF E=2 THEN E2=40/4
571 IF E=3 THEN E2=40/16
572 IF 100*RND(0)<100-(E2) THEN 582
573 PRINT: PRINT "SERIOUS ILLNESS, YOU MUST"
574 PRINT "STOP FOR MEDICAL ATTENTION": PRINT
575 M1=M1-10
576 S4=1
577 GOTO 585
578 PRINT: PRINT "WILD ILLNESS, MEDICINE USED": PRINT
579 M=M-5
580 M1=M1-2
581 GOTO 585
582 PRINT: PRINT "BAD ILLNESS, MEDICINE USED": PRINT
583 M=M-5
584 M1=M1-5
585 IF M1<0 THEN 462
586 IF L1=1 THEN 446
587 GOTO 423
588 END
